#!/usr/bin/env just --justfile
KEEP_TERRAGRUNT_CACHE := "false"

_default:
  @just --list

_terragrunt-run action module *flags:
  terragrunt run-all {{action}} {{flags}} --terragrunt-working-dir {{module}}

clean env:
  @rm -rf ./{{env}}/role.hcl
  @{{KEEP_TERRAGRUNT_CACHE}} || ( find . -type f -name '.terraform*' && find . -type d -name '.terragrunt*' ) | xargs rm -rf

template env: (clean env)
  project_name="$TPL_PROJECT_NAME" \
  github_org="$TPL_GITHUB_ORG" \
  repository_name="$TPL_REPOSITORY_NAME" \
  vault_addr="$TPL_VAULT_ADDR" \
  aws_account_id="$TPL_AWS_ACCOUNT_ID" \
  cluster_domain="$TPL_CLUSTER_DOMAIN" \
  management_node_group_name="$TPL_MANAGEMENT_NODE_GROUP_NAME" \
  management_node_group_role="$TPL_MANAGEMENT_NODE_GROUP_ROLE" \
  role="{{env}}" \
  envsubst < ./{{env}}/role.tpl > ./{{env}}/role.hcl

validate env module: (template env)
  just _terragrunt-run validate {{module}} --terragrunt-non-interactive

plan env module: (validate env module)
  just _terragrunt-run plan {{module}} --terragrunt-non-interactive

apply env module: (plan env module)
  just _terragrunt-run apply {{module}} --terragrunt-non-interactive

destroy env module: (validate env module)
  just _terragrunt-run destroy {{module}} --terragrunt-non-interactive
